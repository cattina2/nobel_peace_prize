# Cours du 15 février 2025

Cours sur l'utilisation d'Allegrograph, selon la recette donnée par le professeur. Voici les requêtes que nous avons utilisées dans le cadre de ce cours. 
Ceci concerne l'importation d'ouvrages au sujet/écrits par les personnes de ma liste, à savoir les nominés au prix Nobel de la Paix. 

## Inspecter les données à importer
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?person ?label
WHERE {
SERVICE <https://dbpedia.org/sparql> {
    dbr:List_of_individuals_nominated_for_the_Nobel_Peace_Prize ?p ?person.
    ?person a dbo:Person;
            dbo:birthDate ?birthDate ;
        rdfs:label ?label.
        BIND(xsd:integer(SUBSTR(STR(?birthDate), 1, 4)) AS ?birthYear)
        FILTER ( ?birthYear > 1770  && LANG(?label) = 'en')
    }
}

## Construire les données à importer

PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>


CONSTRUCT {?person rdfs:label ?label;
                    a dbo:Person;
                    dbo:birthYear ?birthYear.
            }
WHERE {
SERVICE <https://dbpedia.org/sparql> {
    dbr:List_of_individuals_nominated_for_the_Nobel_Peace_Prize ?p ?person.
    ?person a dbo:Person;
            dbo:birthDate ?birthDate ;
        rdfs:label ?label.
        BIND(xsd:integer(SUBSTR(STR(?birthDate), 1, 4)) AS ?birthYear)
        FILTER ( ?birthYear > 1770  && LANG(?label) = 'en')
    }    
}

## Requête pour insérer les triplets

PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>


INSERT {?person rdfs:label ?label;
                    a dbo:Person;
                    dbo:birthYear ?birthYear.
            }
WHERE {
SERVICE <https://dbpedia.org/sparql> {
    dbr:List_of_individuals_nominated_for_the_Nobel_Peace_Prize ?p ?person.
    ?person a dbo:Person;
            dbo:birthDate ?birthDate ;
        rdfs:label ?label.
        BIND(xsd:integer(SUBSTR(STR(?birthDate), 1, 4)) AS ?birthYear)
        FILTER ( ?birthYear > 1770  && LANG(?label) = 'en')
    }    
}

## Inspecter les triplets

PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT ?person  ?wikidataUri
    WHERE {

    ?person a dbo:Person.

    SERVICE <https://dbpedia.org/sparql> {
        ?person owl:sameAs ?wikidataUri.
            FILTER (CONTAINS(STR(?wikidataUri), 'wikidata'))
        }
}

## Insérer les triplets

PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

#CONSTRUCT {?person owl:sameAs ?wikidataUri.}
INSERT {?person owl:sameAs ?wikidataUri.}
    WHERE {

    ?person a dbo:Person.

    SERVICE <https://dbpedia.org/sparql> {
        ?person owl:sameAs ?wikidataUri.
            FILTER (CONTAINS(STR(?wikidataUri), 'wikidata'))
        }
}

## Inspecter les données importées

PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT ?s ?o
WHERE {?s owl:sameAs ?o. }


## Aller cherches dans Wikidata les identifiants IdRef

PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>


SELECT *
WHERE {
  ?s owl:sameAs ?wdPerson;
          a dbo:Person.
  FILTER (CONTAINS(STR(?wdPerson), 'wikidata'))        
  SERVICE <https://query.wikidata.org/sparql> {
            ?wdPerson wdt:P269 ?idRef.
            BIND(URI(CONCAT('http://www.idref.fr/', ?idRef, '/id')) as ?uriIdRef)
      }     
}
LIMIT 10

## Explorer les informations disponibles sur SUDOC
### Tester les URI
select *
where {
    SERVICE <https://data.idref.fr/sparql> {
            <http://www.idref.fr/032799128/id> ?p ?o.
      }     
      }

### Insérer les triplets d'alignement DPBepdia 

PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>

CONSTRUCT {?person owl:sameAs ?uriIdRef.}
#INSERT {?person owl:sameAs ?uriIdRef.}
    WHERE {

      ?person owl:sameAs ?wdPerson;
            a dbo:Person.
    FILTER (CONTAINS(STR(?wdPerson), 'wikidata'))        
    SERVICE <https://query.wikidata.org/sparql> {
              ?wdPerson wdt:P269 ?idRef.
              BIND(URI(CONCAT('http://www.idref.fr/', ?idRef, '/id')) as ?uriIdRef)
        }  
  }

  ### Compter 

  PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>


SELECT ?p (count(*) as ?number)
WHERE {
        ?person owl:sameAs ?idrefPerson;
            a dbo:Person.
    FILTER (CONTAINS(STR(?idrefPerson), 'idref'))  
     
  SERVICE <https://data.idref.fr/sparql> {
            ?idrefPerson ?p ?o.
      }     
  }
GROUP BY ?p
ORDER BY DESC(?number)

Ceci me donne, pour mes nominés au prix Nobel, 3165 livres écrits à leur sujet et 1431 écrits par leur plume. 